apply plugin: 'me.champeau.gradle.japicmp'

Projects.applyOsgi(
  project,
  "Export-Package: okhttp3.tls",
  "Automatic-Module-Name: okhttp3.tls",
  "Bundle-SymbolicName: com.squareup.okhttp3.tls"
)

dependencies {
  api Dependencies.okio
  implementation project(':okhttp')
  compileOnly Dependencies.jsr305
  compileOnly Dependencies.animalSniffer

  testImplementation project(':okhttp-testing-support')
  testImplementation project(':mockwebserver-junit5')
  testImplementation Dependencies.junit
  testImplementation Dependencies.assertj
}

afterEvaluate { project ->
  project.tasks.dokka {
    outputDirectory = "$rootDir/docs/4.x"
    outputFormat = 'gfm'
  }
}

animalsniffer {
  // InsecureExtendedTrustManager (API 24+)
  ignore = ["javax.net.ssl.X509ExtendedTrustManager"]
}

task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask, dependsOn: 'jar') {
  oldClasspath = files(Projects.baselineJar(project))
  newClasspath = files(jar.archivePath)
  onlyBinaryIncompatibleModified = true
  failOnModification = true
  txtOutputFile = file("$buildDir/reports/japi.txt")
  ignoreMissingClasses = true
  includeSynthetic = true
  packageExcludes = [
      'okhttp3.tls.internal',
  ]
}
check.dependsOn(japicmp)
