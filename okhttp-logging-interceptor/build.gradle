apply plugin: 'me.champeau.gradle.japicmp'

Projects.applyOsgi(
  project,
  "Export-Package: okhttp3.logging",
  "Automatic-Module-Name: okhttp3.logging",
  "Bundle-SymbolicName: com.squareup.okhttp3.logging"
)

dependencies {
  api project(':okhttp')
  compileOnly Dependencies.jsr305

  testCompileOnly Dependencies.jsr305
  testImplementation Dependencies.junit
  testImplementation project(':mockwebserver')
  testImplementation project(':mockwebserver-junit5')
  testImplementation project(':okhttp-testing-support')
  testImplementation project(':okhttp-tls')
  testImplementation Dependencies.assertj
}

afterEvaluate { project ->
  project.tasks.dokka {
    outputDirectory = "$rootDir/docs/4.x"
    outputFormat = 'gfm'
  }
}

task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask, dependsOn: 'jar') {
  oldClasspath = files(Projects.baselineJar(project))
  newClasspath = files(jar.archivePath)
  onlyBinaryIncompatibleModified = true
  failOnModification = true
  txtOutputFile = file("$buildDir/reports/japi.txt")
  ignoreMissingClasses = true
  includeSynthetic = true
}
check.dependsOn(japicmp)
